#include "Assets/Shaders/Include/Shared.hlsl"
#include "Assets/Shaders/Include/RayTracingShared.hlsl"

#include "Assets/Shaders/NRD/NRD.hlsli"

#pragma max_recursion_depth 1

// Input
StructuredBuffer<uint4> gIn_ScramblingRanking;
StructuredBuffer<uint4> gIn_Sobol;
Texture2D<float3> gIn_PrevComposedDiff;
Texture2D<float4> gIn_PrevComposedSpec_PrevViewZ;

// RTXDI：上一帧 GBuffer
Texture2D<float> gIn_PrevViewZ;
Texture2D<float4> gIn_PrevNormalRoughness;
Texture2D<float4> gIn_PrevBaseColorMetalness;

// Output
RWTexture2D<float3> g_Output;

// 运动矢量（Motion Vector），用于描述像素在当前帧与上一帧之间的运动，以及视深（ViewZ）和TAA遮罩信息。
RWTexture2D<float4> gOut_Mv;
// 视空间深度（ViewZ），即像素在视空间中的Z值。
RWTexture2D<float> gOut_ViewZ;
// 法线、粗糙度和材质ID的打包信息。用于后续的去噪和材质区分。
RWTexture2D<float4> gOut_Normal_Roughness;
// 基础色（BaseColor，已转为sRGB）和金属度（Metalness）。
RWTexture2D<float4> gOut_BaseColor_Metalness;

// 路径追踪累积通量（Path Traced Throughput），用于存储路径追踪过程中光线的累积贡献。
RWTexture2D<float3> gOut_PsrThroughput;

// 直接光照（Direct Lighting），即主光线命中点的直接光照结果。
RWTexture2D<float3> gOut_DirectLighting;
// 直接自发光（Direct Emission），即材质的自发光分量。
RWTexture2D<float3> gOut_DirectEmission;

// 阴影数据（Shadow Data），如半影宽度等，用于软阴影和去噪。
RWTexture2D<float> gOut_ShadowData;
// 漫反射光照结果（Diffuse Radiance），包含噪声和打包后的信息。
RWTexture2D<float4> gOut_Diff;
// 高光反射光照结果（Specular Radiance），包含噪声和打包后的信息。
RWTexture2D<float4> gOut_Spec;

#include "Assets/Shaders/Rtxdi/RtxdiParameters.h"
#include "Assets/Shaders/donut/packing.hlsli"
#include "Assets/Shaders/donut/brdf.hlsli"


// RTXDI resources
StructuredBuffer<RAB_LightInfo> t_LightDataBuffer;
Buffer<float2> t_NeighborOffsets;

RWStructuredBuffer<RTXDI_PackedDIReservoir> u_LightReservoirs;

#define RTXDI_LIGHT_RESERVOIR_BUFFER u_LightReservoirs
#define RTXDI_NEIGHBOR_OFFSETS_BUFFER t_NeighborOffsets

#define BACKGROUND_DEPTH 65504.f

#define RTXDI_ENABLE_PRESAMPLING 0

#include "RtxdiApplicationBridge/RtxdiApplicationBridge.hlsl"
//
#include "Assets/Shaders/RTXDI/DI/InitialSampling.hlsl"
#include <Assets/Shaders/RTXDI/DI/SpatioTemporalResampling.hlsl>

cbuffer ResamplingConstants
{
    // RTXDI_ReservoirBufferParameters restirDIReservoirBufferParams;

    uint32_t reservoirBlockRowPitch;
    uint32_t reservoirArrayPitch;

    uint32_t pad1;
    uint32_t pad2;

    uint32_t inputBufferIndex;
    uint32_t outputBufferIndex;

    uint neighborOffsetMask;
    uint32_t pad3;
}


// 所有射灯直接光照的累加结果（不经 NRD 降噪，在 Composition 直接叠加）
// RWTexture2D<float3> gOut_SpotDirect;

float2 GetBlueNoise(uint2 pixelPos, uint seed = 0)
{
    // 缓存效率低 多0.2ms
    return Rng::Hash::GetFloat2();
    // https://eheitzresearch.wordpress.com/772-2/
    // https://belcour.github.io/blog/research/publication/2019/06/17/sampling-bluenoise.html

    // Sample index
    uint sampleIndex = (gFrameIndex + seed) & (BLUE_NOISE_TEMPORAL_DIM - 1);

    // sampleIndex = 3;
    // pixelPos /= 8;

    uint2 uv = pixelPos & (BLUE_NOISE_SPATIAL_DIM - 1);
    uint index = uv.x + uv.y * BLUE_NOISE_SPATIAL_DIM;
    uint3 A = gIn_ScramblingRanking[index].xyz;

    // return float2(A.x/256.0 , A.y / 256.0);
    uint rankedSampleIndex = sampleIndex ^ A.z;
    // return float2(rankedSampleIndex / float(BLUE_NOISE_TEMPORAL_DIM), 0);

    uint4 B = gIn_Sobol[rankedSampleIndex & 255];
    float4 blue = (float4(B ^ A.xyxy) + 0.5) * (1.0 / 256.0);

    // ( Optional ) Randomize in [ 0; 1 / 256 ] area to get rid of possible banding
    uint d = Sequence::Bayer4x4ui(pixelPos, gFrameIndex);
    float2 dither = (float2(d & 3, d >> 2) + 0.5) * (1.0 / 4.0);
    blue += (dither.xyxy - 0.5) * (1.0 / 256.0);

    return saturate(blue.xy);
}

float4 GetRadianceFromPreviousFrame(GeometryProps geometryProps, MaterialProps materialProps, uint2 pixelPos)
{
    // Reproject previous frame
    float3 prevLdiff, prevLspec;
    float prevFrameWeight = ReprojectIrradiance(true, false, gIn_PrevComposedDiff, gIn_PrevComposedSpec_PrevViewZ, geometryProps, pixelPos, prevLdiff, prevLspec);

    // Estimate how strong lighting at hit depends on the view direction
    float diffuseProbabilityBiased = EstimateDiffuseProbability(geometryProps, materialProps, true);
    float3 prevLsum = prevLdiff + prevLspec * diffuseProbabilityBiased;

    float diffuseLikeMotion = lerp(diffuseProbabilityBiased, 1.0, Math::Sqrt01(materialProps.curvature)); // TODO: review
    prevFrameWeight *= diffuseLikeMotion;

    float a = Color::Luminance(prevLdiff);
    float b = Color::Luminance(prevLspec);
    prevFrameWeight *= lerp(diffuseProbabilityBiased, 1.0, (a + NRD_EPS) / (a + b + NRD_EPS));

    // Avoid really bad reprojection
    return NRD_MODE < OCCLUSION ? float4(prevLsum * saturate(prevFrameWeight / 0.001), prevFrameWeight) : 0.0;
}


float GetMaterialID(GeometryProps geometryProps, MaterialProps materialProps)
{
    bool isHair = geometryProps.Has(FLAG_HAIR);
    bool isMetal = materialProps.metalness > 0.5;

    return isHair ? MATERIAL_ID_HAIR : (isMetal ? MATERIAL_ID_METAL : MATERIAL_ID_DEFAULT);
}

//========================================================================================
// TRACE OPAQUE
//========================================================================================

/*
The function has not been designed to trace primary hits. But still can be used to trace
direct and indirect lighting.

Prerequisites:
    Rng::Hash::Initialize( )

Derivation:
    Lsum = L0 + BRDF0 * ( L1 + BRDF1 * ( L2 + BRDF2 * ( L3 +  ... ) ) )

    Lsum = L0 +
        L1 * BRDF0 +
        L2 * BRDF0 * BRDF1 +
        L3 * BRDF0 * BRDF1 * BRDF2 +
        ...
*/


struct TraceOpaqueResult
{
    float3 diffRadiance;
    float diffHitDist;

    float3 specRadiance;
    float specHitDist;

    float3 debug;
};


TraceOpaqueResult TraceOpaque(GeometryProps geometryProps0, MaterialProps materialProps0, uint2 pixelPos, float3x3 mirrorMatrix, float4 Lpsr)
{
    TraceOpaqueResult result = (TraceOpaqueResult)0;
    #if( NRD_MODE < OCCLUSION )
    result.specHitDist = NRD_FrontEnd_SpecHitDistAveraging_Begin();
    #endif

    // viewZ0 and NRD demodulation factors MUST use the original primary surface
    // so that they match the G-Buffer written in MainRayGenShader.
    float viewZ0 = Geometry::AffineTransform(gWorldToView, geometryProps0.X).z;
    float roughness0 = materialProps0.roughness;

    // Material de-modulation ( convert irradiance into radiance )
    float3 diffFactor0, specFactor0;
    {
        float3 albedo, Rf0;
        BRDF::ConvertBaseColorMetalnessToAlbedoRf0(materialProps0.baseColor, materialProps0.metalness, albedo, Rf0);

        NRD_MaterialFactors(materialProps0.N, geometryProps0.V, albedo, Rf0, materialProps0.roughness, diffFactor0, specFactor0);

        // We can combine radiance ( for everything ) and irradiance ( for hair ) in denoising if material ID test is enabled
        if (geometryProps0.Has(FLAG_HAIR) && NRD_NORMAL_ENCODING == NRD_NORMAL_ENCODING_R10G10B10A2_UNORM)
        {
            diffFactor0 = 1.0;
            specFactor0 = 1.0;
        }
    }

    // SHARC debug visualization
    #if( USE_SHARC_DEBUG != 0 )
    HashGridParameters hashGridParams;
    hashGridParams.cameraPosition = gCameraGlobalPos.xyz;
    hashGridParams.sceneScale = SHARC_SCENE_SCALE;
    hashGridParams.logarithmBase = SHARC_GRID_LOGARITHM_BASE;
    hashGridParams.levelBias = SHARC_GRID_LEVEL_BIAS;

    SharcHitData sharcHitData;
    sharcHitData.positionWorld = GetGlobalPos(geometryProps0.X);
    sharcHitData.materialDemodulation = GetMaterialDemodulation(geometryProps0, materialProps0);
    sharcHitData.normalWorld = geometryProps0.N;
    sharcHitData.emissive = materialProps0.Lemi;

    HashMapData hashMapData;
    hashMapData.capacity = SHARC_CAPACITY;
    hashMapData.hashEntriesBuffer = gInOut_SharcHashEntriesBuffer;

    SharcParameters sharcParams;
    sharcParams.gridParameters = hashGridParams;
    sharcParams.hashMapData = hashMapData;
    sharcParams.radianceScale = SHARC_RADIANCE_SCALE;
    sharcParams.enableAntiFireflyFilter = SHARC_ANTI_FIREFLY;
    sharcParams.accumulationBuffer = gInOut_SharcAccumulated;
    sharcParams.resolvedBuffer = gInOut_SharcResolved;

    #if( USE_SHARC_DEBUG == 2 )
    result.diffRadiance = HashGridDebugColoredHash(sharcHitData.positionWorld, sharcHitData.normalWorld, hashGridParams);
    #else
    bool isValid = SharcGetCachedRadiance(sharcParams, sharcHitData, result.diffRadiance, true);

    // Highlight invalid cells
    // result.diffRadiance = isValid ?  result.diffRadiance : float3( 1.0, 0.0, 0.0 );
    #endif

    // result.diffRadiance /= diffFactor0;

    return result;
    #endif


    // uint checkerboard = Sequence::CheckerBoard(pixelPos, g_FrameIndex) != 0;

    uint checkerboard = Sequence::CheckerBoard(pixelPos, gFrameIndex) != 0;
    uint pathNum = gSampleNum << (gTracingMode == RESOLUTION_FULL ? 1 : 0);
    uint diffPathNum = 0;

    [loop]
    for (uint path = 0; path < pathNum; path++)
    {
        GeometryProps geometryProps = geometryProps0;
        MaterialProps materialProps = materialProps0;

        float accumulatedHitDist = 0;
        float accumulatedDiffuseLikeMotion = 0;
        float accumulatedCurvature = 0;

        float3 Lsum = Lpsr.xyz;
        float3 pathThroughput = 1.0 - Lpsr.w;
        bool isDiffusePath = false;

        [loop]
        for (uint bounce = 1; bounce <= gBounceNum && !geometryProps.IsMiss(); bounce++)
        {
            //=============================================================================================================================================================
            // Origin point
            //=============================================================================================================================================================

            bool isDiffuse = false;
            float lobeTanHalfAngleAtOrigin = 0.0;
            {
                // Diffuse probability
                float diffuseProbability = EstimateDiffuseProbability(geometryProps, materialProps);

                float rnd = Rng::Hash::GetFloat();
                if (gTracingMode == RESOLUTION_FULL_PROBABILISTIC && bounce == 1 && !gRR)
                {
                    // Clamp probability to a sane range to guarantee a sample in 3x3 ( or 5x5 ) area ( see NRD docs )
                    diffuseProbability = float(diffuseProbability != 0.0) * clamp(diffuseProbability, gMinProbability, 1.0 - gMinProbability);
                    rnd = Sequence::Bayer4x4(pixelPos, gFrameIndex) + rnd / 16.0;
                }

                // Diffuse or specular?
                isDiffuse = rnd < diffuseProbability; // TODO: if "diffuseProbability" is clamped, "pathThroughput" should be adjusted too
                if (gTracingMode == RESOLUTION_FULL_PROBABILISTIC || bounce > 1)
                    pathThroughput /= isDiffuse ? diffuseProbability : (1.0 - diffuseProbability);
                else
                    isDiffuse = gTracingMode == RESOLUTION_HALF ? checkerboard : (path & 0x1);

                // This is not needed in case of "RESOLUTION_FULL_PROBABILISTIC", since hair doesn't have diffuse component
                if (geometryProps.Has(FLAG_HAIR) && isDiffuse)
                    break;

                // Importance sampling
                uint sampleMaxNum = 0;
                if (bounce == 1 && gDisableShadowsAndEnableImportanceSampling && NRD_MODE < OCCLUSION)
                    sampleMaxNum = PT_IMPORTANCE_SAMPLES_NUM * (isDiffuse ? 1.0 : GetSpecMagicCurve(materialProps.roughness));
                sampleMaxNum = max(sampleMaxNum, 1);

                #if( NRD_MODE < OCCLUSION )
                float2 rnd2 = Rng::Hash::GetFloat2();
                #else
                uint2 blueNoisePos = pixelPos + uint2(Sequence::Weyl2D(0.0, path * gBounceNum + bounce) * (BLUE_NOISE_SPATIAL_DIM - 1));
                float2 rnd2 = GetBlueNoise(blueNoisePos, gTracingMode == RESOLUTION_HALF);
                #endif

                float3 ray = GenerateRayAndUpdateThroughput(geometryProps, materialProps, pathThroughput, sampleMaxNum, isDiffuse, rnd2, HAIR);

                // Special case for primary surface ( 1st bounce starts here )
                if (bounce == 1)
                {
                    isDiffusePath = isDiffuse;

                    if (gTracingMode == RESOLUTION_FULL)
                        Lsum *= isDiffuse ? diffuseProbability : (1.0 - diffuseProbability);

                    // ( Optional ) Save sampling direction for the 1st bounce
                    #if( NRD_MODE == SH )
                    float3 psrRay = Geometry::RotateVectorInverse(mirrorMatrix, ray);

                    if (isDiffuse)
                        result.diffDirection += psrRay;
                    else
                        result.specDirection += psrRay;
                    #endif
                }

                // Abort tracing if the current bounce contribution is low
                #if( USE_RUSSIAN_ROULETTE == 1 )
                /*
                BAD PRACTICE:
                Russian Roulette approach is here to demonstrate that it's a bad practice for real time denoising for the following reasons:
                - increases entropy of the signal
                - transforms radiance into non-radiance, which is strictly speaking not allowed to be processed spatially (who wants to get a high energy firefly
                redistributed around surrounding pixels?)
                - not necessarily converges to the right image, because we do assumptions about the future and approximate the tail of the path via a scaling factor
                - this approach breaks denoising, especially REBLUR, which has been designed to work with pure radiance
                */

                // Nevertheless, RR can be used with caution: the code below tuned for good IQ / PERF tradeoff
                float russianRouletteProbability = Color::Luminance(pathThroughput);
                russianRouletteProbability = Math::Pow01(russianRouletteProbability, 0.25);
                russianRouletteProbability = max(russianRouletteProbability, 0.01);

                if (Rng::Hash::GetFloat() > russianRouletteProbability)
                    break;

                pathThroughput /= russianRouletteProbability;
                #else
                /*
                GOOD PRACTICE:
                - terminate path if "pathThroughput" is smaller than some threshold
                - approximate ambient at the end of the path
                - re-use data from the previous frame
                */

                if (PT_THROUGHPUT_THRESHOLD != 0.0 && Color::Luminance(pathThroughput) < PT_THROUGHPUT_THRESHOLD)
                    break;
                #endif

                //=========================================================================================================================================================
                // Trace to the next hit
                //=========================================================================================================================================================

                float roughnessTemp = isDiffuse ? 1.0 : materialProps.roughness;
                lobeTanHalfAngleAtOrigin = roughnessTemp * roughnessTemp / (1.0 + roughnessTemp * roughnessTemp);

                float2 mipAndCone = GetConeAngleFromRoughness(geometryProps.mip, isDiffuse ? 1.0 : materialProps.roughness);

                CastRay(geometryProps.GetXoffset(geometryProps.N), ray, 0.0, INF, mipAndCone, FLAG_NON_TRANSPARENT, geometryProps, materialProps);
            }

            //=============================================================================================================================================================
            // Hit point
            //=============================================================================================================================================================

            {
                //=============================================================================================================================================================
                // Lighting
                //=============================================================================================================================================================

                float4 Lcached = float4(materialProps.Lemi, 0.0);
                if (!geometryProps.IsMiss())
                {
                    // L1 cache - reproject previous frame, carefully treating specular
                    Lcached = GetRadianceFromPreviousFrame(geometryProps, materialProps, pixelPos);

                    // L2 cache - SHARC
                    HashGridParameters hashGridParams;
                    hashGridParams.cameraPosition = gCameraGlobalPos.xyz;
                    hashGridParams.sceneScale = SHARC_SCENE_SCALE;
                    hashGridParams.logarithmBase = SHARC_GRID_LOGARITHM_BASE;
                    hashGridParams.levelBias = SHARC_GRID_LEVEL_BIAS;

                    float3 Xglobal = GetGlobalPos(geometryProps.X);
                    uint level = HashGridGetLevel(Xglobal, hashGridParams);
                    float voxelSize = HashGridGetVoxelSize(level, hashGridParams);

                    float footprint = geometryProps.hitT * lobeTanHalfAngleAtOrigin * 2.0;
                    float footprintNorm = saturate(footprint / voxelSize);

                    float2 rndScaled = ImportanceSampling::Cosine::GetRay(Rng::Hash::GetFloat2()).xy;
                    rndScaled *= 1.0 - footprintNorm; // reduce dithering if cone is already wide
                    rndScaled *= voxelSize;
                    rndScaled *= USE_SHARC_DITHERING;

                    float3x3 mBasis = Geometry::GetBasis(geometryProps.N);
                    Xglobal += mBasis[0] * rndScaled.x + mBasis[1] * rndScaled.y;

                    SharcHitData sharcHitData;
                    sharcHitData.positionWorld = Xglobal;
                    sharcHitData.materialDemodulation = GetMaterialDemodulation(geometryProps, materialProps);
                    sharcHitData.normalWorld = geometryProps.N;
                    sharcHitData.emissive = materialProps.Lemi;

                    HashMapData hashMapData;
                    hashMapData.capacity = SHARC_CAPACITY;
                    hashMapData.hashEntriesBuffer = gInOut_SharcHashEntriesBuffer;

                    SharcParameters sharcParams;
                    sharcParams.gridParameters = hashGridParams;
                    sharcParams.hashMapData = hashMapData;
                    sharcParams.radianceScale = SHARC_RADIANCE_SCALE;
                    sharcParams.enableAntiFireflyFilter = SHARC_ANTI_FIREFLY;
                    sharcParams.accumulationBuffer = gInOut_SharcAccumulated;
                    sharcParams.resolvedBuffer = gInOut_SharcResolved;

                    bool isSharcAllowed = !geometryProps.Has(FLAG_HAIR); // ignore if the hit is hair // TODO: if hair don't allow if hitT is too short
                    isSharcAllowed &= Rng::Hash::GetFloat() > Lcached.w; // is needed?
                    isSharcAllowed &= Rng::Hash::GetFloat() < (bounce == gBounceNum ? 1.0 : footprintNorm); // is voxel size acceptable?
                    isSharcAllowed &= gSHARC && NRD_MODE < OCCLUSION; // trivial

                    float3 sharcRadiance;
                    if (isSharcAllowed && SharcGetCachedRadiance(sharcParams, sharcHitData, sharcRadiance, false))
                        Lcached = float4(sharcRadiance, 1.0);

                    // Cache miss - compute lighting, if not found in caches
                    if (Rng::Hash::GetFloat() > Lcached.w)
                    {
                        float3 L = GetLighting(geometryProps, materialProps, LIGHTING | SHADOW) + materialProps.Lemi;
                        Lcached.xyz = bounce < gBounceNum ? L : max(Lcached.xyz, L);
                    }
                }

                //=============================================================================================================================================================
                // Other
                //=============================================================================================================================================================

                // Accumulate lighting
                float3 L = Lcached.xyz * pathThroughput;
                Lsum += L;

                // ( Biased ) Reduce contribution of next samples if previous frame is sampled, which already has multi-bounce information
                pathThroughput *= 1.0 - Lcached.w;

                // Accumulate path length for NRD ( see "README/NOISY INPUTS" )
                float a = Color::Luminance(L);
                float b = Color::Luminance(Lsum); // already includes L
                float importance = a / (b + 1e-6);

                importance *= 1.0 - Color::Luminance(materialProps.Lemi) / (a + 1e-6);

                float diffuseLikeMotion = EstimateDiffuseProbability(geometryProps, materialProps, true);
                diffuseLikeMotion = isDiffuse ? 1.0 : diffuseLikeMotion;

                accumulatedHitDist += ApplyThinLensEquation(geometryProps.hitT, accumulatedCurvature) * Math::SmoothStep(0.2, 0.0, accumulatedDiffuseLikeMotion);
                accumulatedDiffuseLikeMotion += 1.0 - importance * (1.0 - diffuseLikeMotion);
                accumulatedCurvature += materialProps.curvature; // yes, after hit

                #if( USE_CAMERA_ATTACHED_REFLECTION_TEST == 1 && NRD_NORMAL_ENCODING == NRD_NORMAL_ENCODING_R10G10B10A2_UNORM )
                // IMPORTANT: lazy ( no checkerboard support ) implementation of reflections masking for objects attached to the camera
                // TODO: better find a generic solution for tracking of reflections for objects attached to the camera
                if (bounce == 1 && !isDiffuse && desc.materialProps.roughness < 0.01)
                {
                    if (!geometryProps.IsMiss() && !geometryProps.Has(FLAG_STATIC))
                        gOut_Normal_Roughness[desc.pixelPos].w = MATERIAL_ID_SELF_REFLECTION;
                }
                #endif
            }
        }

        // Debug visualization: specular mip level at the end of the path
        if (gOnScreen == SHOW_MIP_SPECULAR)
        {
            float mipNorm = Math::Sqrt01(geometryProps.mip / MAX_MIP_LEVEL);
            Lsum = Color::ColorizeZucconi(mipNorm);
        }

        // Normalize hit distances for REBLUR before averaging ( needed only for AO for REFERENCE )
        float normHitDist = accumulatedHitDist;
        if (gDenoiserType != DENOISER_RELAX)
            normHitDist = REBLUR_FrontEnd_GetNormHitDist(accumulatedHitDist, viewZ0, gHitDistParams, isDiffusePath ? 1.0 : materialProps0.roughness);

        // Accumulate diffuse and specular separately for denoising
        if (!USE_SANITIZATION || NRD_IsValidRadiance(Lsum))
        {
            if (isDiffusePath)
            {
                result.diffRadiance += Lsum;
                result.diffHitDist += normHitDist;
                diffPathNum++;
            }
            else
            {
                result.specRadiance += Lsum;

                #if( NRD_MODE < OCCLUSION )
                NRD_FrontEnd_SpecHitDistAveraging_Add(result.specHitDist, normHitDist);
                #else
                result.specHitDist += normHitDist;
                #endif
            }
        }
    }

    // Material de-modulation ( convert irradiance into radiance )
    if (gOnScreen != SHOW_MIP_SPECULAR)
    {
        result.diffRadiance /= diffFactor0;
        result.specRadiance /= specFactor0;
    }

    // Radiance is already divided by sampling probability, we need to average across all paths
    float radianceNorm = 1.0 / float(gSampleNum);
    result.diffRadiance *= radianceNorm;
    result.specRadiance *= radianceNorm;

    // Others are not divided by sampling probability, we need to average across diffuse / specular only paths
    float diffNorm = diffPathNum == 0 ? 0.0 : 1.0 / float(diffPathNum);
    float specNorm = pathNum == diffPathNum ? 0.0 : 1.0 / float(pathNum - diffPathNum);

    result.diffHitDist *= diffNorm;

    #if( NRD_MODE < OCCLUSION )
    NRD_FrontEnd_SpecHitDistAveraging_End(result.specHitDist);
    #else
    result.specHitDist *= specNorm;
    #endif

    #if( NRD_MODE == SH || NRD_MODE == DIRECTIONAL_OCCLUSION )
    result.diffDirection *= diffNorm;
    result.specDirection *= specNorm;
    #endif

    return result;
}

//========================================================================================
// MAIN
//========================================================================================

void WriteResult(uint2 pixelPos, float4 diff, float4 spec, float4 diffSh, float4 specSh)
{
    uint2 outPixelPos = pixelPos;
    if (gTracingMode == RESOLUTION_HALF)
        outPixelPos.x >>= 1;

    uint checkerboard = Sequence::CheckerBoard(pixelPos, gFrameIndex) != 0;

    if (gTracingMode == RESOLUTION_HALF)
    {
        if (checkerboard)
        {
            gOut_Diff[outPixelPos] = diff;

            #if( NRD_MODE == SH )
            gOut_DiffSh[outPixelPos] = diffSh;
            #endif
        }
        else
        {
            gOut_Spec[outPixelPos] = spec;

            #if( NRD_MODE == SH )
            gOut_SpecSh[outPixelPos] = specSh;
            #endif
        }
    }
    else
    {
        gOut_Diff[outPixelPos] = diff;
        gOut_Spec[outPixelPos] = spec;

        #if( NRD_MODE == SH )
        gOut_DiffSh[outPixelPos] = diffSh;
        gOut_SpecSh[outPixelPos] = specSh;
        #endif
    }
}

[shader("raygeneration")]
void MainRayGenShader()
{
    uint2 pixelPos = DispatchRaysIndex().xy;

    float2 pixelUv = float2(pixelPos + 0.5) / gRectSize;
    float2 sampleUv = pixelUv + gJitter;

    if (pixelUv.x > 1.0 || pixelUv.y > 1.0)
    {
        #if( USE_DRS_STRESS_TEST == 1 )
        WriteResult(pixelPos, GARBAGE, GARBAGE, GARBAGE, GARBAGE);
        #endif

        return;
    }

    // Initialize RNG
    Rng::Hash::Initialize(pixelPos, gFrameIndex);

    //================================================================================================================================================================================
    // Primary ray
    //================================================================================================================================================================================

    float3 cameraRayOrigin = 0;
    float3 cameraRayDirection = 0;
    GetCameraRay(cameraRayOrigin, cameraRayDirection, sampleUv);

    GeometryProps geometryProps0;
    MaterialProps materialProps0;
    CastRay(cameraRayOrigin, cameraRayDirection, 0.0, 1000.0, GetConeAngleFromRoughness(0.0, 0.0), (gOnScreen == SHOW_INSTANCE_INDEX || gOnScreen == SHOW_NORMAL) ? GEOMETRY_ALL : FLAG_NON_TRANSPARENT, geometryProps0, materialProps0);

    //================================================================================================================================================================================
    // Primary surface replacement ( aka jump through mirrors )
    //================================================================================================================================================================================

    float3 psrThroughput = 1.0;
    float3x3 mirrorMatrix = Geometry::GetMirrorMatrix(0); // identity
    float accumulatedHitDist = 0.0;
    float accumulatedCurvature = 0.0;
    uint bounceNum = PT_PSR_BOUNCES_NUM;


    float3 X0 = geometryProps0.X;
    float3 V0 = geometryProps0.V;
    float viewZ0 = Geometry::AffineTransform(gWorldToView, geometryProps0.X).z;

    bool isTaa5x5 = geometryProps0.Has(FLAG_HAIR | FLAG_SKIN) || geometryProps0.IsMiss(); // switched TAA to "higher quality & slower response" mode
    float viewZAndTaaMask0 = abs(viewZ0) * FP16_VIEWZ_SCALE * (isTaa5x5 ? -1.0 : 1.0);

    [loop]
    while (bounceNum && !geometryProps0.IsMiss() && IsDelta(materialProps0) && gPSR)
    {
        {
            // Origin point
            // Accumulate curvature
            accumulatedCurvature += materialProps0.curvature; // yes, before hit

            // Accumulate mirror matrix
            mirrorMatrix = mul(Geometry::GetMirrorMatrix(materialProps0.N), mirrorMatrix);

            // Choose a ray
            float3 ray = reflect(-geometryProps0.V, materialProps0.N);

            // Update throughput
            float3 albedo, Rf0;
            BRDF::ConvertBaseColorMetalnessToAlbedoRf0(materialProps0.baseColor, materialProps0.metalness, albedo, Rf0);

            float NoV = abs(dot(materialProps0.N, geometryProps0.V));
            float3 Fenv = BRDF::EnvironmentTerm_Rtg(Rf0, NoV, materialProps0.roughness);

            psrThroughput *= Fenv;

            // Trace to the next hit
            float2 mipAndCone = GetConeAngleFromRoughness(geometryProps0.mip, materialProps0.roughness);


            CastRay(geometryProps0.GetXoffset(geometryProps0.N), ray, 0.0, INF, mipAndCone, GEOMETRY_ALL, geometryProps0, materialProps0);
        }

        {
            // Hit point
            // Accumulate hit distance representing virtual point position ( see "README/NOISY INPUTS" )
            accumulatedHitDist += ApplyThinLensEquation(geometryProps0.hitT, accumulatedCurvature); // TODO: take updated from NRD
        }

        bounceNum--;
    }

    //================================================================================================================================================================================
    // G-buffer ( guides )
    //================================================================================================================================================================================

    // Motion
    float3 Xvirtual = X0 - V0 * accumulatedHitDist;
    float3 XvirtualPrev = Xvirtual + geometryProps0.Xprev - geometryProps0.X;
    float3 motion = GetMotion(Xvirtual, XvirtualPrev);

    gOut_Mv[pixelPos] = float4(motion, viewZAndTaaMask0); // IMPORTANT: keep viewZ before PSR ( needed for glass )

    // ViewZ
    float viewZ = Geometry::AffineTransform(gWorldToView, Xvirtual).z;
    viewZ = geometryProps0.IsMiss() ? Math::Sign(viewZ) * INF : viewZ;

    gOut_ViewZ[pixelPos] = viewZ;

    // Emission
    gOut_DirectEmission[pixelPos] = materialProps0.Lemi * psrThroughput;

    // Early out
    if (geometryProps0.IsMiss())
    {
        #if( USE_INF_STRESS_TEST == 1 )
        WriteResult(pixelPos, GARBAGE, GARBAGE, GARBAGE, GARBAGE);
        #endif

        return;
    }

    // Normal, roughness and material ID
    float3 N = Geometry::RotateVectorInverse(mirrorMatrix, materialProps0.N);
    float materialID = GetMaterialID(geometryProps0, materialProps0);
    #if( USE_SIMULATED_MATERIAL_ID_TEST == 1 )
    materialID = frac(geometryProps0.X).x < 0.05 ? MATERIAL_ID_HAIR : materialID;
    #endif

    gOut_Normal_Roughness[pixelPos] = NRD_FrontEnd_PackNormalAndRoughness(N, materialProps0.roughness, materialID);

    // Base color and metalness
    gOut_BaseColor_Metalness[pixelPos] = float4(Color::ToSrgb(materialProps0.baseColor), materialProps0.metalness);

    // Direct lighting
    // float3 Xshadow;
    float3 Ldirect = GetLighting(geometryProps0, materialProps0, LIGHTING | SHADOW | SSS);

    if (gOnScreen == SHOW_INSTANCE_INDEX)
    {
        Rng::Hash::Initialize(geometryProps0.instanceIndex, 0);

        uint checkerboard = Sequence::CheckerBoard(pixelPos >> 2, 0) != 0;
        float3 color = Rng::Hash::GetFloat4().xyz;
        color *= (checkerboard && !geometryProps0.Has(FLAG_STATIC)) ? 0.5 : 1.0;

        Ldirect = color;
    }
    else if (gOnScreen == SHOW_UV)
        Ldirect = float3(0, 0, 0);
    else if (gOnScreen == SHOW_CURVATURE)
        Ldirect = sqrt(abs(materialProps0.curvature)) * 0.1;
    else if (gOnScreen == SHOW_MIP_PRIMARY)
    {
        float mipNorm = Math::Sqrt01(geometryProps0.mip / MAX_MIP_LEVEL);
        Ldirect = Color::ColorizeZucconi(mipNorm);
    }

    gOut_DirectLighting[pixelPos] = Ldirect; // "psrThroughput" applied in "Composition"

    // gOut_DirectLighting[pixelPos] = gIn_PrevBaseColorMetalness[pixelPos].xyz  - gOut_BaseColor_Metalness[pixelPos];

    if (geometryProps0.primitiveIndex == INF)
    {
        gOut_DirectLighting[pixelPos] = 0;
    }
    else
    {
        // float3 ll = float3(geometryProps0.primitiveIndex % 10 / 10.0, (geometryProps0.primitiveIndex) % 10 / 10.0, (geometryProps0.primitiveIndex) % 10 / 10.0);

        RAB_LightInfo lightInfo = t_LightDataBuffer[geometryProps0.primitiveIndex];
        float3 ll = Unpack_R16G16B16A16_FLOAT(lightInfo.radiance);
        gOut_DirectLighting[pixelPos] = ll;
    }
    // gOut_SpotDirect[pixelPos] = EvaluateSpotLights(geometryProps0, materialProps0);
    // gOut_SpotDirect[pixelPos] = 0;
    gOut_PsrThroughput[pixelPos] = psrThroughput;

    // Lighting at PSR hit, if found
    float4 Lpsr = 0;
    if (!geometryProps0.IsMiss() && bounceNum != PT_PSR_BOUNCES_NUM)
    {
        // L1 cache - reproject previous frame, carefully treating specular
        Lpsr = GetRadianceFromPreviousFrame(geometryProps0, materialProps0, pixelPos);

        // Subtract direct lighting, process it separately
        float3 L = Ldirect + materialProps0.Lemi;
        Lpsr.xyz = max(Lpsr.xyz - L, 0.0);

        // TODO: it's not a 100% fix
        if (gTracingMode == RESOLUTION_HALF && (gIndirectDiffuse + gIndirectSpecular) > 1.5)
            Lpsr *= 0.5;

        // This is important!
        Lpsr.xyz *= Lpsr.w;
    }

    //================================================================================================================================================================================
    // Secondary rays
    //================================================================================================================================================================================

    TraceOpaqueResult result = TraceOpaque(geometryProps0, materialProps0, pixelPos, mirrorMatrix, Lpsr);

    #if( USE_MOVING_EMISSION_FIX == 1 )
    // Or emissives ( not having lighting in diffuse and specular ) can use a different material ID
    result.diffRadiance += materialProps0.Lemi / Math::Pi(2.0);
    result.specRadiance += materialProps0.Lemi / Math::Pi(2.0);
    #endif


    // Test RTXDI

    RAB_RandomSamplerState rng = RAB_InitRandomSampler(pixelPos, 1);

    RTXDI_DIReservoir reservoir = RTXDI_EmptyDIReservoir();


    RTXDI_SampleParameters sampleParams = RTXDI_InitSampleParameters(
         gSssTransmissionBsdfSampleCount, // local light samples 
        0, // infinite light samples
        0, // environment map samples
        gSsTransmissionPerBsdfScatteringSampleCount,
        0,
        0.001f);

    // RTXDI_SampleParameters sampleParams = RTXDI_InitSampleParameters(
    //     g_numInitialSamples, // local light samples 
    //     0, // infinite light samples
    //     0, // environment map samples
    //     g_numInitialBRDFSamples,
    //     g_brdfCutoff,
    //     0.001f);


    RTXDI_LightBufferParameters lightBufferParams = (RTXDI_LightBufferParameters)0;


    lightBufferParams.localLightBufferRegion.firstLightIndex = 0;
    lightBufferParams.localLightBufferRegion.numLights = 3964;
    lightBufferParams.infiniteLightBufferRegion.firstLightIndex = 0;
    lightBufferParams.infiniteLightBufferRegion.numLights = 0;
    lightBufferParams.environmentLightParams.lightIndex = RTXDI_INVALID_LIGHT_INDEX;
    lightBufferParams.environmentLightParams.lightPresent = false;

    RAB_Surface primarySurface = RAB_EmptySurface();
    primarySurface.worldPos = geometryProps0.X;

    primarySurface.viewDir = geometryProps0.V;
    primarySurface.viewDepth = -viewZ0;
    primarySurface.normal = materialProps0.N;
    primarySurface.geoNormal = geometryProps0.N;

    RAB_Material material = RAB_EmptyMaterial();

    float3 albedo, Rf0;
    BRDF::ConvertBaseColorMetalnessToAlbedoRf0(materialProps0.baseColor, materialProps0.metalness, albedo, Rf0);

    material.diffuseAlbedo = albedo;
    material.specularF0 = Rf0;
    material.roughness = materialProps0.roughness;

    primarySurface.material = material;

    primarySurface.diffuseProbability = getSurfaceDiffuseProbability(primarySurface);


    // Generate the initial sample
    RAB_LightSample lightSample = RAB_EmptyLightSample();
    RTXDI_DIReservoir localReservoir = RTXDI_SampleLocalLights(rng, rng, primarySurface, sampleParams, ReSTIRDI_LocalLightSamplingMode_UNIFORM, lightBufferParams.localLightBufferRegion, lightSample);



    
    RTXDI_CombineDIReservoirs(reservoir, localReservoir, 0.5, localReservoir.targetPdf);
    
    
    // Resample BRDF samples.
    RAB_LightSample brdfSample = RAB_EmptyLightSample();
    RTXDI_DIReservoir brdfReservoir = RTXDI_SampleBrdf(rng, primarySurface, sampleParams, lightBufferParams, brdfSample);
    bool selectBrdf = RTXDI_CombineDIReservoirs(reservoir, brdfReservoir, RAB_GetNextRandom(rng), brdfReservoir.targetPdf);
    if (selectBrdf)
    {
        lightSample = brdfSample;
    }

    RTXDI_FinalizeResampling(reservoir, 1.0, 1.0);
    reservoir.M = 1;

    // BRDF was generated with a trace so no need to trace visibility again
    // BRDF 是通过追踪生成的，因此无需再次追踪可见性
    if (RTXDI_IsValidDIReservoir(reservoir) && !selectBrdf)
    {
        // See if the initial sample is visible from the surface
        // 查看初始样本对于表面是否可见
        if (!RAB_GetConservativeVisibility(primarySurface, lightSample))
        {
            // If not visible, discard the sample (but keep the M)
            // 如果不可见，则丢弃样本（但保留 M 值）
            RTXDI_StoreVisibilityInDIReservoir(reservoir, 0, true);
        }
    }
    
    
    RTXDI_ReservoirBufferParameters restirDIReservoirBufferParams;
    
    restirDIReservoirBufferParams.reservoirBlockRowPitch = reservoirBlockRowPitch;
    restirDIReservoirBufferParams.reservoirArrayPitch = reservoirArrayPitch;
    
    {
        RTXDI_DISpatioTemporalResamplingParameters stparams;
        stparams.screenSpaceMotion = motion;
        stparams.sourceBufferIndex = inputBufferIndex;
        stparams.maxHistoryLength = 20;
        stparams.biasCorrectionMode = RTXDI_BIAS_CORRECTION_BASIC;
        stparams.depthThreshold = 0.1;
        stparams.normalThreshold = 0.5;
        stparams.numSamples = 1 + 1;
        stparams.numDisocclusionBoostSamples = 0;
        stparams.samplingRadius = 32;
        stparams.enableVisibilityShortcut = true;
        stparams.enablePermutationSampling = true;
        stparams.discountNaiveSamples = false;
    
    
        // This variable will receive the position of the sample reused from the previous frame.
        // It's only needed for gradient evaluation, ignore it here.
        int2 temporalSamplePixelPos = -1;
    
    
        RTXDI_RuntimeParameters runtimeParams;
    
        runtimeParams.neighborOffsetMask = neighborOffsetMask;
        runtimeParams.activeCheckerboardField = 0;
    
    
        // Call the resampling function, update the reservoir and lightSample variables
        reservoir = RTXDI_DISpatioTemporalResampling(pixelPos, primarySurface, reservoir,
                                                     rng, runtimeParams, restirDIReservoirBufferParams, stparams, temporalSamplePixelPos, lightSample);
    }
    
    
    float3 shadingOutput = 0;
    
    
    // Shade the surface with the selected light sample
    // 使用选定的光照样本对表面进行着色
    if (RTXDI_IsValidDIReservoir(reservoir))
    {
        // Compute the correctly weighted reflected radiance
        // 计算正确加权的反射辐射亮度
        shadingOutput = ShadeSurfaceWithLightSample(lightSample, primarySurface)
            * RTXDI_GetDIReservoirInvPdf(reservoir);
    
        // Test if the selected light is visible from the surface
        // 测试选定的光源对于表面是否可见
        bool visibility = RAB_GetConservativeVisibility(primarySurface, lightSample);
    
        // If not visible, discard the shading output and the light sample
        // 如果不可见，则丢弃着色输出和光照样本
        if (!visibility)
        {
            shadingOutput = 0;
            RTXDI_StoreVisibilityInDIReservoir(reservoir, 0, true);
        }
    }
    
    
    shadingOutput += materialProps0.Lemi;
    shadingOutput = basicToneMapping(shadingOutput, 0.005);
    

    gOut_DirectLighting[pixelPos] = float4(shadingOutput, 1.0);


    RTXDI_StoreDIReservoir(reservoir, restirDIReservoirBufferParams, pixelPos, outputBufferIndex);

    // END of test RTXDI

    // //================================================================================================================================================================================
    // // Sun shadow
    // //================================================================================================================================================================================
    // geometryProps0.X = Xshadow;
    //
    // float2 rnd = GetBlueNoise(pixelPos);
    // rnd = ImportanceSampling::Cosine::GetRay(rnd).xy;
    // rnd *= gTanSunAngularRadius;
    //
    // float3 sunDirection = normalize(gSunBasisX.xyz * rnd.x + gSunBasisY.xyz * rnd.y + gSunDirection.xyz);
    // float3 Xoffset = geometryProps0.GetXoffset(sunDirection, PT_SHADOW_RAY_OFFSET);
    // float2 mipAndCone = GetConeAngleFromAngularRadius(geometryProps0.mip, gTanSunAngularRadius);
    //
    // float shadowTranslucency = (Color::Luminance(Ldirect) != 0.0) ? 1.0 : 0.0;
    // float shadowHitDist = 0.0;
    //
    // if (shadowTranslucency > 0.1)
    // {
    //     if (gRR)
    //     {
    //         float hitT = CastVisibilityRay_AnyHit( Xoffset, sunDirection, 0.0, INF, mipAndCone, gWorldTlas,FLAG_NON_TRANSPARENT,0);
    //         shadowHitDist = hitT;
    //     }
    //     else
    //     {
    //         GeometryProps geometryPropsShadow;
    //         MaterialProps materialPropsShadow;
    //
    //         CastRay(Xoffset, sunDirection, 0.0, INF, mipAndCone, FLAG_NON_TRANSPARENT, geometryPropsShadow, materialPropsShadow);
    //
    //         shadowHitDist = geometryPropsShadow.hitT;
    //     }
    // }
    // shadowHitDist = INF;

    float penumbra = SIGMA_FrontEnd_PackPenumbra(INF, gTanSunAngularRadius);

    gOut_ShadowData[pixelPos] = penumbra;

    //================================================================================================================================================================================
    // Output
    //================================================================================================================================================================================

    float4 outDiff = 0.0;
    float4 outSpec = 0.0;
    float4 outDiffSh = 0.0;
    float4 outSpecSh = 0.0;

    if (gDenoiserType == DENOISER_RELAX)
    {
        #if( NRD_MODE == SH )
        outDiff = RELAX_FrontEnd_PackSh(result.diffRadiance, result.diffHitDist, result.diffDirection, outDiffSh, USE_SANITIZATION);
        outSpec = RELAX_FrontEnd_PackSh(result.specRadiance, result.specHitDist, result.specDirection, outSpecSh, USE_SANITIZATION);
        #else
        outDiff = RELAX_FrontEnd_PackRadianceAndHitDist(result.diffRadiance, result.diffHitDist, USE_SANITIZATION);
        outSpec = RELAX_FrontEnd_PackRadianceAndHitDist(result.specRadiance, result.specHitDist, USE_SANITIZATION);
        #endif
    }
    else
    {
        #if( NRD_MODE == OCCLUSION )
        outDiff = result.diffHitDist;
        outSpec = result.specHitDist;
        #elif( NRD_MODE == SH )
        outDiff = REBLUR_FrontEnd_PackSh(result.diffRadiance, result.diffHitDist, result.diffDirection, outDiffSh, USE_SANITIZATION);
        outSpec = REBLUR_FrontEnd_PackSh(result.specRadiance, result.specHitDist, result.specDirection, outSpecSh, USE_SANITIZATION);
        #elif( NRD_MODE == DIRECTIONAL_OCCLUSION )
        outDiff = REBLUR_FrontEnd_PackDirectionalOcclusion(result.diffDirection, result.diffHitDist, USE_SANITIZATION);
        #else
        outDiff = REBLUR_FrontEnd_PackRadianceAndNormHitDist(result.diffRadiance, result.diffHitDist, USE_SANITIZATION);
        outSpec = REBLUR_FrontEnd_PackRadianceAndNormHitDist(result.specRadiance, result.specHitDist, USE_SANITIZATION);
        #endif
    }

    WriteResult(pixelPos, outDiff, outSpec, outDiffSh, outSpecSh);
}
